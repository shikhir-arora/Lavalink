buildscript {
    ext {
        springBootVersion = '2.0.2.RELEASE'
        gradleGitVersion = '1.5.1'
        sonarqubeVersion = '2.6.2'
    }
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarqubeVersion}"
    }
}

allprojects {

    repositories {
        mavenCentral()                     // main maven repo
        jcenter()                          // JDA and some other stuff
        mavenLocal()                       // useful for developing
        maven { url "https://jitpack.io" } // build projects directly from github 
    }

    group = 'lavalink'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    if (project.hasProperty("includeAnalysis")) {
        project.logger.lifecycle("applying analysis plugins")
        apply from: "../analysis.gradle"
    }

    compileJava.dependsOn 'clean'
    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    ext {
        //@formatter:off

        lavaplayerVersion               = '1.3.7'
        magmaVersion                    = '0.4.4'
        jdaNasVersion                   = '1.0.6'
        jappVersion                     = '1.2'
        jdaVersion                      = '3.6.0_367'

        springBootVersion               = "${springBootVersion}"
        javaWebSocketVersion            = '1.3.8'
        logbackVersion                  = '1.2.3'
        slf4jVersion                    = '1.7.25'
        sentryLogbackVersion            = '1.7.0'
        oshiVersion                     = '3.5.0'
        jsonOrgVersion                  = '20180130'
        spotbugsAnnotationsVersion      = '3.1.3'
        prometheusVersion               = '0.4.0'
        commonsLangVersion              = '3.7'

        junitJupiterVersion             = '5.2.0'
        junitPlatformVersion            = '1.2.0'
        unirestVersion                  = '1.4.9'

        //@formatter:on
    }
}

ext {
    moduleName = 'Lavalink-Parent'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
    //noinspection UnnecessaryQualifiedReference
    distributionType = Wrapper.DistributionType.ALL
}
